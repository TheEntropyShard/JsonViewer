plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.gmazzo.buildconfig' version '5.3.5'
}

group = 'me.theentropyshard'
version = '0.7.3'
description = 'JsonViewer'

def theMainClass = 'me.theentropyshard.jsonviewer.Main'
def theVersion = project.version.toString()
def theName = project.name

repositories {
    mavenCentral()
}

ext {
    flatlaf = '3.4.1'
    gson = '2.10.1'
    rsyntaxtextarea = '3.4.0'
    miglayout = '5.3'
    junit = '5.7.2'
    okhttp = '4.12.0'
}

dependencies {
    implementation "com.formdev:flatlaf:$flatlaf"
    implementation "com.formdev:flatlaf-extras:$flatlaf"
    implementation "com.google.code.gson:gson:$gson"
    implementation "com.fifesoft:rsyntaxtextarea:$rsyntaxtextarea"
    implementation "com.miglayout:miglayout-swing:$miglayout"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit"
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = theMainClass
}

jar {
    manifest {
        attributes(
                'Main-Class': theMainClass,
                'Implementation-Title': theName,
                'Implementation-Version': theVersion,
                'Implementation-Vendor': theName
        )
    }
}

shadowJar {
    archiveClassifier = ''
    archiveBaseName = theName
    archiveVersion = theVersion
}

buildConfig {
    packageName('me.theentropyshard.jsonviewer')
    className('BuildConfig')
    useJavaOutput()

    buildConfigField(String, 'APP_NAME', theName)
    buildConfigField(String, 'APP_VERSION', theVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}
